#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Full
Input = Stream
Parser = LL
#RegularDefinitions
L  : [A-Za-z]
D  : [0-9]
WS : [\ \t\n\r] //simbolos especiais e branco
COMMENT : "(*" [^ "*" ]* "*)"  //comentário

#Tokens
//pontuação
"("
")"
","
";"
"."
":"
":="
"="
"<"
">"
">="
"<="
"<>"
"+"
"-"
"*"
"/"

//tokens
id : {L} ( {L} | {D} | _ )*
num : {D}+ //um ou mais dígitos
literal: ( {L} | {D} ) * //nao eh a definicao correta, mas o lexico do gals nao eh usado

//palavras chave
program = id : "program"
const = id : "const"
var = id : "var"
procedure = id : "procedure"
begin = id : "begin"
end = id : "end"
integer = id : "integer"
call = id : "call"
if = id : "if"
then = id : "then"
else = id : "else"
while = id : "while"
do = id : "do"
repeat = id : "repeat"
until = id : "until"
readln = id : "readln"
writeln = id : "writeln"
or = id : "or"
and = id : "and"
not = id : "not"
for = id : "for"
to = id : "to"
case = id : "case"
of = id : "of"


//ignorar espaços em branco, caracteres especiais e comentários
 : {WS}*
 :! {COMMENT}

#NonTerminals
<PROGRAMA>
<BLOCO>
<LID>
<REPIDENT>
<DCLCONST>
<LDCONST>
<DCLVAR>
<LDVAR>
<TIPO>
<DCLPROC>
<DEFPAR>
<CORPO>
<REPCOMANDO>
<COMANDO>
<PARAMETROS>
<REPPAR>
<ELSEPARTE>
<VARIAVEL>
<REPVARIAVEL>
<ITEMSAIDA>
<REPITEM>
<EXPRESSAO>
<REPEXPSIMP>
<EXPSIMP>
<REPEXP>
<TERMO>
<REPTERMO>
<FATOR>
<CONDCASE>
<CONTCASE>
<RPINTEIRO>
#Grammar
<PROGRAMA> ::= program id #23 ";" <BLOCO> "." #24;
<BLOCO> ::= <DCLCONST> <DCLVAR> #25 <DCLPROC> <CORPO>;
<LID> ::= id #27 <REPIDENT>;
<REPIDENT> ::= î;
<REPIDENT> ::= "," id #27 <REPIDENT>;
<DCLCONST> ::= const id #28 "=" num #29 ";" <LDCONST>;
<LDCONST> ::= î;
<LDCONST> ::= id #28 "=" num #29 ";" <LDCONST>;
<DCLCONST> ::= î;
<DCLVAR> ::= var #30 <LID> ":" <TIPO> ";" <LDVAR>;
<LDVAR> ::= î;
<LDVAR> ::= <LID> ":" <TIPO> ";" <LDVAR>;
<DCLVAR> ::= î;
<TIPO> ::= integer;
<DCLPROC> ::= procedure id #31 <DEFPAR> ";" #32 <BLOCO> ";" #33 <DCLPROC> ;
<DCLPROC> ::= î;
<DEFPAR> ::= î;
<DEFPAR> ::= "(" #34 <LID> ":" integer ")";
<CORPO> ::= begin <COMANDO> <REPCOMANDO> end;
<REPCOMANDO> ::= î;
<REPCOMANDO> ::= ";" <COMANDO> <REPCOMANDO>;
<COMANDO> ::= id #37 ":=" <EXPRESSAO> #38;
<COMANDO> ::= <CORPO>;
<COMANDO> ::= î;
<COMANDO> ::= call id #39 <PARAMETROS> #40;
<PARAMETROS> ::= î;
<PARAMETROS> ::= "(" <EXPRESSAO> #41 <REPPAR> ")";
<REPPAR> ::= î;
<REPPAR> ::= "," <EXPRESSAO> #41 <REPPAR>;
<COMANDO> ::= if <EXPRESSAO> #43 then <COMANDO> <ELSEPARTE> #44;
<ELSEPARTE> ::= î;
<ELSEPARTE> ::= #45 else <COMANDO>;
<COMANDO> ::= while #46 <EXPRESSAO> #47 do <COMANDO> #48;
<COMANDO> ::= repeat #49 <COMANDO> until <EXPRESSAO> #50;
<COMANDO> ::= readln #51 "(" <VARIAVEL> <REPVARIAVEL> ")";
<VARIAVEL> ::= id #52;
<REPVARIAVEL>::= î;
<REPVARIAVEL>::= "," <VARIAVEL> <REPVARIAVEL>;
<COMANDO> ::= writeln "(" <ITEMSAIDA> <REPITEM> ")";
<ITEMSAIDA> ::= literal #53;
<ITEMSAIDA> ::= <EXPRESSAO> #54;
<REPITEM> ::= î;
<REPITEM> ::= "," <ITEMSAIDA> <REPITEM>;
<COMANDO> ::= case #55 <EXPRESSAO> of <CONDCASE> end #56;
<CONDCASE> ::= num <RPINTEIRO> ":" #57 <COMANDO> #58 <CONTCASE>;
<RPINTEIRO> ::= "," #59 num <RPINTEIRO>;
<RPINTEIRO> ::= î ;
<CONTCASE> ::= î ;
<CONTCASE> ::= ";" <CONDCASE>;
<COMANDO> ::= for id #60 ":=" <EXPRESSAO> #61 to <EXPRESSAO> #62 do <COMANDO> #63 ;
<EXPRESSAO> ::= <EXPSIMP> <REPEXPSIMP>;
<REPEXPSIMP> ::= î;
<REPEXPSIMP> ::= "=" <EXPSIMP> #64 ;
<REPEXPSIMP> ::= "<" <EXPSIMP> #65 ;
<REPEXPSIMP> ::= ">" <EXPSIMP> #66 ;
<REPEXPSIMP> ::= ">=" <EXPSIMP> #67 ;
<REPEXPSIMP> ::= "<=" <EXPSIMP> #68 ;
<REPEXPSIMP> ::= "<>" <EXPSIMP> #69 ;
<EXPSIMP> ::= "+" <TERMO> <REPEXP> ;
<EXPSIMP> ::= "-" <TERMO> #70 <REPEXP> ;
<EXPSIMP> ::= <TERMO> <REPEXP> ; //hmmmmm
<REPEXP> ::= "+" <TERMO> #71 <REPEXP>;
<REPEXP> ::= "-" <TERMO> #72 <REPEXP>;
<REPEXP> ::= or <TERMO> #73 <REPEXP>;
<REPEXP> ::= î;
<TERMO> ::= <FATOR> <REPTERMO>;
<REPTERMO> ::= î;
<REPTERMO> ::= "*" <FATOR> #74 <REPTERMO>;
<REPTERMO> ::= "/" <FATOR> #75 <REPTERMO>;
<REPTERMO> ::= and <FATOR> #76 <REPTERMO>;
<FATOR> ::= num #77;
<FATOR> ::= "(" <EXPRESSAO> ")" ;
<FATOR> ::= not <FATOR> #78 ;
<FATOR> ::= #79 <VARIAVEL>;
